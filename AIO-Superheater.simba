{==============================================================================\
|                            All-in-one Superheater                            |
|                                  By: Coh3n                                   |
|==============================================================================|
|                                                                              |
|      Written By: Coh3n                                                       |
|      Description: Superheats any bar                                         |
|      Contact Info: Private message via Villavu.com                           |
|                                                                              |
|      Many more scripts of various types can be found at VillaVu.com!         |
 \_____________________________________________________________________________}

{==============================================================================\
|                              Setup Instructions                              |
|==============================================================================]
|                                                                              |
|       1. Have Nature runes in backpack slot 28                               |
|       2. Have Coal bag in backpack slot 27 (Coal bag optional)               |
|       3. Be in a bank where the booth is in front of you when compass is     |
|          facing north (I recommend Lumbridge top floor)                      |
|       4. Setup script from lines 81-146                                      |
|       5. Save your changes and hit Run (F9)                                  |
|       6. Post any progress reports and/or bugs!                              |
 \_____________________________________________________________________________}

program AIOSuperheater;
{$DEFINE SMART}
{$i srl-6/srl.simba}

const
  SCRIPT_VERSION = '1.4';

const
  BAR_BRONZE  = 0;
  BAR_IRON    = 1;
  BAR_SILVER  = 2;
  BAR_STEEL   = 3;
  BAR_GOLD    = 4;
  BAR_MITHRIL = 5;
  BAR_ADAMANT = 6;
  BAR_RUNE    = 7;

type
  TBar = record
    name, statsName: string;
    amtPrimary: integer;     // number of primary ore for 1 load
    hasSecondary: boolean;
    expSmithing: extended;
    oreUptext: TStringArray;
    webString: string;
    _profit: integer;
  end;

// _* means changing won't break script, but still shouldn't change
const
  _P_BAR      = 0;
  _P_BREAK    = 1;
  _P_TIME     = 2;
  _P_CUSTOM_X = 3;
  _P_CUSTOM_Y = 4;

  _P_BANK  = 0;

  _P_COAL_BAG = 0;

  // not set by user
  _P_NATURES_START = 10;
  _P_CASTS         = 11;
  _P_FALSE         = 12;

  // bank slots for ores
  _PRIMARY_SLOT   = 9;
  _SECONDARY_SLOT = 10;

// __* means changing will break script
const
  __SLOT_NATURE   = 28; // inventory slot holding the nature runes
  __EXP_SUPERHEAT = 53;
  __UPTEXT_ORE    = ['Ore', 're'];
  __UPTEXT_COAL   = ['Coal', 'oal', 'Co'];

var
  _bars: array of TBar;

{==============================================================================\
|                               Script Setup                                   |
|==============================================================================|
|                                                                              |
|    * To setup the script and your players.                                   |
 \_____________________________________________________________________________}

const
  STATS_ID = '';       // the stats ID you use to login @ stats.villavu.com
  STATS_PASSWORD = ''; // the stats password
  SRL_LOG = true;      // saves SRL debug to a log file
  SRL_DEBUG = true;    // debugs SRL procedures in the debug box
  IP_SCREEN = true;    // takes a screenshot of your last IP in case of hacks

procedure declarePlayers();
begin
  players.setup(['PlayerNames/Nicknames/LoginNames'], 'PlayerFileName');
  currentPlayer := 0;

  with players[0] do
  begin
    booleans[_P_COAL_BAG] := true;        // true = using Coal bag
    integers[_P_BAR]      := BAR_MITHRIL; // bar to make
    integers[_P_BREAK]    := 0;           // how many minutes to break for (leave 0 to not break)
    integers[_P_TIME]     := 0;           // time before breaking (leave 0 to not break)
    integers[_P_CUSTOM_X] := 645;         // X coord of where to click the superheat spell
    integers[_P_CUSTOM_Y] := 391;         // Y coord of where to click the superheat spell
  end;
{
  with players[1] do
  begin
    booleans[_P_COAL_BAG] := true;
    integers[_P_BAR]      := BAR_MITHRIL;
    integers[_P_BREAK]    := 0;
    integers[_P_TIME]     := 0;
  end;

  with players[2] do
  begin
    booleans[_P_COAL_BAG] := true;
    integers[_P_BAR]      := BAR_MITHRIL;
    integers[_P_BREAK]    := 0;
    integers[_P_TIME]     := 0;
  end;

  with players[3] do
  begin
    booleans[_P_COAL_BAG] := true;
    integers[_P_BAR]      := BAR_MITHRIL;
    integers[_P_BREAK]    := 0;
    integers[_P_TIME]     := 0;
  end;

  with players[4] do
  begin
    booleans[_P_COAL_BAG] := true;
    integers[_P_BAR]      := BAR_MITHRIL;
    integers[_P_BREAK]    := 0;
    integers[_P_TIME]     := 0;
  end;
}
end;

{==============================================================================\
|                             End Script Setup                                 |
|==============================================================================}

procedure signature;
begin
  ClearDebug;
  Writeln('                                            ___          ');
  Wait(100);
  Writeln('                       ____                /\__\         ');
  Wait(100);
  Writeln('                      /\___\     ____      \/ _/         ');
  Wait(100);
  Writeln('       _____          \/_  /    /\___\      \/ _____     ');
  Wait(100);
  Writeln('      /\____\  _______/ / /___ _\/___ \_____  /\____\    ');
  Wait(100);;
  Writeln('     / /  ___\/\______\/ /____\\____/ /_____\/ / ____\   ');
  Wait(100);
  Writeln('    / /  /__ / / ___  / ____  //___  / ____  \/ /___\    ');
  Wait(100);
  Writeln('   / /  /___\ / /__/ / / / / /____/ / /  / / /____  /    ');
  Wait(100);
  Writeln('   \/_______//______/_/ /_/_/______/_/__/_/_/____/ /     ');
  Wait(100);
  Writeln('                             \/___________________/      ');
  Wait(100);
  Writeln(PadR('                       All-in-one Superheater ' + SCRIPT_VERSION + '!', 57));
end;

procedure findNormalRandoms();
var
  p: TPoint;
begin
  if (findSpinTicket(p)) then
    mouse(p, MOUSE_LEFT);

  closeAdWindow();
end;

function randomMinutes(min: integer): integer;
begin
  result := randomRange(-min * 60000, min * 60000);
end;

function boolToInt(b: boolean): integer;
begin
  if (b) then
    result := 1
  else
    result := 0;
end;

function getBar(bar: integer): TBar;
begin
  case bar of
    BAR_BRONZE:
      with result do
      begin
        name := 'Bronze Bar';
        statsName := 'Bronze Bars Melted';
        amtPrimary   := 13;
        hasSecondary := true;
        expSmithing  := 6.2;
        oreUptext    := ['Tin', 'in', 'Copper', 'opper'];
        webString    := 'Bronze_bar';
      end;

    BAR_IRON:
      with result do
      begin
        name := 'Iron Bar';
        statsName := 'Iron Bars Melted';
        amtPrimary   := 27;
        hasSecondary := false;
        expSmithing  := 12.5;
        oreUptext    := ['Iron', 'ron', 'Ir'];
        webString    := 'Iron_bar';
      end;

    BAR_SILVER:
      with result do
      begin
        name := 'Silver Bar';
        statsName := 'Silver Bars Melted';
        amtPrimary   := 27;
        hasSecondary := false;
        expSmithing  := 13.7;
        oreUptext    := ['Silver', 'ilver', 'ver'];
        webString    := 'Silver_bar';
      end;

    BAR_STEEL:
      with result do
      begin
        name := 'Steel Bar';
        statsName := 'Steel Bars Melted';
        amtPrimary   := (9 + (boolToInt(players[currentPlayer].booleans[_P_COAL_BAG])) * 8);
        hasSecondary := true;
        expSmithing  := 17.5;
        oreUptext    := ['Iron', 'ron', 'Ir', 'ro', 'n Or'];
        webString    := 'Steel_bar';
      end;

    BAR_GOLD:
      with result do
      begin
        name := 'Gold Bar';
        statsName := 'Gold Bars Melted';
        amtPrimary   := 27;
        hasSecondary := false;
        expSmithing  := 22.5;
        oreUptext    := ['Gold', 'old'];
        webString    := 'Gold_bar';
      end;

    BAR_MITHRIL:
      with result do
      begin
        name := 'Mithril Bar';
        statsName := 'Mithril Bars Melted';
        amtPrimary   := (5 + (boolToInt(players[currentPlayer].booleans[_P_COAL_BAG])) * 5);;
        hasSecondary := true;
        expSmithing  := 30;
        oreUptext    := ['Mithril', 'ithril', 'ril'];
        webString    := 'Mithril_bar';
      end;

    BAR_ADAMANT:
      with result do
      begin
        name := 'Adamant Bar';
        statsName := 'Adamant Bars Melted';
        amtPrimary   := (3 + (boolToInt(players[currentPlayer].booleans[_P_COAL_BAG])) * 4);;
        hasSecondary := true;
        expSmithing  := 37.5;
        oreUptext    := ['Adamant', 'damant', 'ant'];
        webString    := 'Adamant_bar';
      end;

    BAR_RUNE:
      with result do
      begin
        name := 'Rune Bar';
        statsName := 'Runite Bars Melted';
        amtPrimary   := (3 + (boolToInt(players[currentPlayer].booleans[_P_COAL_BAG])) * 2);;
        hasSecondary := true;
        expSmithing  := 50.0;
        oreUptext    := ['Runite', 'unite'];
        webString    := 'Rune_bar';
      end;

    else
      writeln('getBar: Invalid bar selected');
  end;
end;

function getNaturesBank(): integer;
begin
  if (not bankScreen.isOpen()) then
    exit;

  result := getItemAmount(bankscreen.getPackSlotBox(__SLOT_NATURE));

  if (result <= 0) then
    players[currentPlayer].strings[_P_FALSE] := 'No Runes';

  writeln('Natures found bank pack: '+toStr(result));
end;

function getNaturesBackpack(): integer;
begin
  if (not tabBackpack.open()) then
    exit;

  result := getItemAmount(tabBackpack.getSlotBox(__SLOT_NATURE));

  if (result <= 0) then
    players[currentPlayer].strings[_P_FALSE] := 'No Runes';

  writeln('Natures found backpack: '+toStr(result));
end;

function getSmithingExp(bar: TBar; casts: integer): integer;
begin
  result := round(bar.expSmithing * casts);
end;

function getMagicExp(casts: integer): integer;
begin
  result := round(__EXP_SUPERHEAT * casts);
end;

function getProfit(bar: TBar): integer; // credit to euphemism
var
  p, barString, barProfit: string;
begin
  p := getPage('http://runescape.wikia.com/wiki/Calculator:Magic/Superheat');

  barString := between('<a href="/wiki/' + bar.webString + '"', '</tr>', p);
  barProfit := between('>', '<', between('e="c', '/s', barString));
  barProfit := replace(barProfit, ',', '', []);

  result := strToIntDef(barProfit, -1);
  writeln(bar.name+' Profit: '+toStr(result));
end;

function perHour(amt, time: integer): integer;
begin
  if (amt = -1) then
  begin
    result := -1;
    exit;
  end;

  try
    result := round((amt * 60) / (time / 60000));
  except
  end;
end;

const
  __PADR = 44;
  __TOK = ',';
procedure progressReport();
var
  i, _expM, _expS, _worked, _profit: integer;
  _bar: TBar;
begin
  setScriptProp(sp_WriteTimeStamp, [false]);
  writeln('');

  writeln('[-------------------------------------------]'); // 45
  writeln('[            AIO SuperHeater '+SCRIPT_VERSION+'            ]');
  writeln('[                 by Coh3n                  ]');
  writeln('[-------------------------------------------]');
  writeln('[                                           ]');
  writeln(padR('[           '+msToTime(getTimeRunning, TIME_ABBREV), __PADR)+']');
  writeln('[                                           ]');

  for i := 0 to high(players) do
    with players[i] do
    begin
      _bar  := _bars[currentPlayer];
      _expM := getMagicExp(integers[_P_CASTS]);
      _expS := getSmithingExp(_bar, integers[_P_CASTS]);
      _worked := worked.getTime();

      if (_bar._profit > 0) then
        _profit := (_bar._profit * integers[_P_CASTS])
      else
        _profit := -1;

      if (isActive) then
        writeln(padR('[    Player ~ '+toStr(i)+' (True)', __PADR)+']')
      else
        writeln(padR('[    Player ~ '+toStr(i)+' ('+strings[_P_FALSE]+')', __PADR)+']');

      writeln(padR('[      Worked   ~ '+msToTime(_worked, TIME_BARE), __PADR)+']');
      writeln(padR('[      Bar      ~ '+_bar.name, __PADR)+']');
      writeln(padR('[      Casts    ~ '+groupDigits(integers[_P_CASTS], __TOK)+' ('+groupDigits(perHour(integers[_P_CASTS], _worked), __TOK)+' P/H)', __PADR)+']');
      writeln(padR('[      Magic XP ~ '+groupDigits(_expM, __TOK)+' ('+groupDigits(perHour(_expM, _worked), __TOK)+' P/H)', __PADR)+']');
      writeln(padR('[      Smith XP ~ '+groupDigits(_expS, __TOK)+' ('+groupDigits(perHour(_expS, _worked), __TOK)+' P/H)', __PADR)+']');
      writeln(padR('[      Total XP ~ '+groupDigits(_expM+_expS, __TOK)+' ('+groupDigits(perHour((_expM+_expS), _worked), __TOK)+' P/H)', __PADR)+']');
      writeln(padR('[      Profit   ~ '+groupDigits(_profit, __TOK)+' ('+groupDigits(perHour(_profit, _worked), __TOK)+' P/H)', __PADR)+']');

      writeln('[                                           ]');

      disguise('Spells Cast: '+groupDigits(integers[_P_CASTS], __TOK));
    end;

  writeln('[-------------------------------------------]');
  writeln('[-------------------------------------------]');

  setScriptProp(sp_WriteTimeStamp, [false]);
end;

function waitOnInvTab(time: integer): boolean;
var
  t: integer := getSystemTime() + time;
begin
  while ((getSystemTime() < t) and (not result)) do
  begin
    result := tabBackpack.isOpen();
    wait(50);
  end;
end;

function waitOnMagicTab(time: integer): boolean;
var
  t: integer := getSystemTime() + time;
begin
  while ((getSystemTime() < t) and (not result)) do
  begin
    result := gameTabs.isTabActive(TAB_MAGIC);
    wait(50);
  end;
end;

function waitPackCountChanged(oldCount: integer; time: integer): boolean;
var
  t: integer := getSystemTime() + time;
begin
  while ((getSystemTime() < t) and (not result)) do
  begin
    result := (bankscreen.getPackCount() <> oldCount);
    wait(50);
  end;
end;

// Returns false if the player is finished casting (i.e. out of runes)
function setCasts(inBank: boolean): boolean;
var
  nats, prevCasts: integer;
begin
  result := true;

  if (inBank) then
    nats := getNaturesBank()
  else
    nats := getNaturesBackpack();

  if (nats <= 0) then
    nats := 0;

  with players[currentPlayer] do
  begin
    prevCasts := integers[_P_CASTS];

    // failsafe in case there was a getNatures fail (can't cast more than the amount of primary ore)
    if ((nats = 0) and (integers[_P_NATURES_START] - prevCasts > _bars[currentPlayer].amtPrimary)) then
      exit;

    integers[_P_CASTS] := (integers[_P_NATURES_START] - nats);
    statsIncVariable(_bars[currentPlayer].statsName, (integers[_P_CASTS] - prevCasts));

    if (integers[_P_CASTS] >= integers[_P_NATURES_START]) then
    begin
      result := false;
      strings[_P_FALSE] := 'Finished';
    end;
  end;
end;

function depositJunk(bar: TBar): boolean;
var
  endSlot: integer;
begin
  endSlot := (bar.amtPrimary + boolToInt(players[currentPlayer].booleans[_P_COAL_BAG]));
  result := bankscreen.deposit([1..endSlot]);
end;

function withdrawOres(bar: TBar): boolean;
var
  nats, t, amt: integer;
begin
  nats := getNaturesBank();
  if (nats <= 0) then
    exit;

  if (nats >= bar.amtPrimary) then
  begin
    amt := (bar.amtPrimary * boolToInt(bar.amtPrimary <> 27)); // either bar.amtPrimary or 0

    if (amt = 0) then
      bankscreen.withdraw(_PRIMARY_SLOT, WITHDRAW_AMOUNT_ALL, __UPTEXT_ORE)
    else
      bankscreen.withdraw(_PRIMARY_SLOT, amt, __UPTEXT_ORE)

  end else begin
    // withdraw the amount of runes we have left
    _bars[currentPlayer].amtPrimary := nats;
    bankscreen.withdraw(_PRIMARY_SLOT, nats, __UPTEXT_ORE);
  end;

  if (bar.hasSecondary) then
    bankscreen.withdraw(_SECONDARY_SLOT, WITHDRAW_AMOUNT_ALL, __UPTEXT_COAL);

  t := (getSystemTime() + 3000);
  while (getSystemTime() < t) do
  begin
    if (isItemIn(bankscreen.getPackSlotBox(_bars[currentPlayer].amtPrimary + 1))) then
    begin
      result := true;
      break;
    end;

    wait(50);
  end;
end;

function fillCoalBag(): boolean;
var
  p: TPoint;
  oldCount: integer;
begin
  if (not players[currentPlayer].booleans[_P_COAL_BAG]) then
    exit;

  if (bankscreen.withdraw(_SECONDARY_SLOT, WITHDRAW_AMOUNT_ALL, __UPTEXT_COAL)) then
  begin
    mouseBox(bankscreen.getPackSlotBox(27), MOUSE_MOVE);
    oldCount := bankscreen.getPackCount();
    fastClick(MOUSE_RIGHT);

    if (chooseOption.select(['Fill', 'ill', 'll'], 500)) then
    begin
      getMousePos(p.x, p.y);
      mouse(point(p.x - 175 - random(50), p.y - 50 - random(20)), MOUSE_MOVE); // gets rid of other spell info. that may appear after clicking "Fill"
      result := waitPackCountChanged(oldCount, 3000);
    end;
  end;
end;

function openBank(): boolean;
var
  c: integer;
  t: integer := (getSystemTime() + 3000);
begin
  if (not isLoggedIn()) then
    exit;

  mouse(point(mainscreen.playerBox.x1 + 15, mainscreen.playerBox.y1 - 20).rand(10), MOUSE_MOVE);

  // a failsafe in case it tries to cast a spell on the bank booth
  if (isMouseOverText(['Cast', 'ast', 'heat', 'item'])) then
  begin
    fastClick(MOUSE_LEFT);
    wait(50 + random(50));
  end;

  repeat
    fastClick(MOUSE_LEFT);

    while (getSystemTime() < t) and (not result) do
    begin
      result := bankscreen.isOpen();
      wait(50);
    end;

    inc(c);
  until(result or (c >= 3));
end;

function bank(bar: TBar): boolean;
begin
  if (not isLoggedIn()) then
    exit;

  findNormalRandoms();

  if (openBank()) then
  begin
    wait(200 + random(200));
    depositJunk(bar);

    if (not setCasts(true)) then // if player is finished casting
      exit;

    if (not fillCoalBag()) then
      depositJunk(bar);

    if (withdrawOres(bar)) then
    begin
      result := true;
      clearDebug();
      progressReport();
      bankscreen.close();
    end;

  end else
    writeln('bank: Failed to open '+players[currentPlayer].strings[_P_BANK]);
end;

function myCast(): boolean;
var
  p: TPoint;
begin
  with players[currentPlayer] do
    if (gameTabs.openTab(TAB_MAGIC)) then
    begin
      getMousePos(p.x, p.y);

      if (inRange(p.x, integers[_P_CUSTOM_X] - 3, integers[_P_CUSTOM_X] + 3) and
          inRange(p.y, integers[_P_CUSTOM_Y] - 3, integers[_P_CUSTOM_Y] + 3)) then // so the mouse doesn't move only a couple pixels when casting
        mouse(p, MOUSE_LEFT)
      else
        mouse(point(integers[_P_CUSTOM_X] + randomRange(-3, 3), integers[_P_CUSTOM_Y]), MOUSE_LEFT);

      result := true;
    end;
end;

function castSlot(slot: integer; bar: TBar): boolean;
var
  x, y, mouseSlot: integer;
  successful: boolean = false;
begin
  if (not isLoggedIn()) then
    exit;

  if (not myCast()) then
    exit;

  if (waitOnInvTab(2000)) then
  begin
    getMousePos(x, y);
    mouseSlot := tabBackpack.pointToSlot(point(x, y));

    if (slot > mouseSlot) or (not isMouseOverText(bar.oreUptext, 300)) then
      tabBackpack.mouseSlot(bar.amtPrimary, MOUSE_MOVE);

    if (isMouseOverText(bar.oreUptext, 300)) then
    begin
      getMousePos(x, y);
      mouse(point(x, y), MOUSE_LEFT);
      successful := true;
    end else
      writeln('castSlot: Failed to find mouseOverText "'+toStr(bar.oreUptext)+'"');
  end;

  if (successful) then
    result := waitOnMagicTab(2000);
end;

function castLoad(bar: TBar): boolean;
var
  i, c, t: integer;
begin
  for i := 1 to (bar.amtPrimary) do
    repeat
      inc(t);

      if (castSlot(i, bar)) then // last slot ore appears
      begin
        t := 0;
        inc(c);
        break;
      end;

      if (getNaturesBackpack() <= 0) then // if run out of runes
        exit;

      if (t >= 3) then // something went wrong, exit
        exit;

    until(not isLoggedIn());

  result := (c = (bar.amtPrimary));
end;

procedure takeBreak();
var
  t, breakTime: integer;
begin
  if (length(players) > 1) then
    players.next(true)
  else begin
    breakTime := (players[currentPlayer].integers[_P_BREAK] * 60000) + randomMinutes(5);

    if (breakTime >= (30 * 60000)) then
      players[currentPlayer].logout()
    else
      players[currentPlayer].exitToLobby();

    t := (getSystemTime + (breakTime));
    writeln('Breaking for '+msToTime(t - getSystemTime, TIME_BARE));

    while (getSystemTime < t) do
    begin
      writeln('Time left: '+msToTime(t - getSystemTime, TIME_BARE));
      wait(randomRange(10000, 20000));
    end;

    players[currentPlayer].login();
  end;

  findNormalRandoms();
end;

function setupPlayer(): boolean;
begin
  if (players[currentPlayer].login()) then
  begin
    minimap.clickCompass();
    mainscreen.setAngle(MS_ANGLE_HIGH);
    result := true;
    findNormalRandoms();
    players[currentPlayer].integers[_P_NATURES_START] := getNaturesBackpack();
  end;
end;

procedure setupScript();
var
  i: integer;
begin
  // set SRL variables
  declarePlayers();

  for i := 0 to high(players) do
    if (not players[i].isActive) then
      players[i].Strings[_P_FALSE] := 'User set false';

  setLength(_bars, length(players));
end;

procedure next(why: string = '');
begin
  findNormalRandoms();

  players[currentPlayer].isActive := false;

  if (players[currentPlayer].strings[_P_FALSE] = '') then
    players[currentPlayer].strings[_P_FALSE] := why;

  if ((length(players) <= 1) or (players.getActive <= 1)) then
  begin
    bankscreen.close();
    tabBackpack.waitWhileLocked();
    setCasts(false);

    players[currentPlayer].logout();
    writeln('Player '+toStr(currentPlayer)+': '+why);
    terminateScript();
  end else
    players.next(false);
end;

procedure onLogin();
begin
  exitSquealOfFortune();
  findNormalRandoms();
end;

procedure mainloop();
var
  tmp: TTimeMarker;
begin
  repeat
    if (not setupPlayer()) then
    begin
      next('Login fail');
      continue;
    end;

    _bars[currentPlayer] := getBar(players[currentPlayer].integers[_P_BAR]);
    _bars[currentPlayer]._profit := getProfit(_bars[currentPlayer]);
    tmp.start();

    repeat
      if (not bank(_bars[currentPlayer])) then
        if (not bank(_bars[currentPlayer])) then // retry
          next('Failed to bank');

      with players[currentPlayer] do
        if (integers[_P_TIME] > 0) then
          if (tmp.getTime() >= (integers[_P_TIME] * 60000 + randomMinutes(5))) then
          begin
            takeBreak();
            tmp.reset();
            tmp.start();
          end;

      if (not castLoad(_bars[currentPlayer])) then
        if (not setCasts(false)) then // if finished
          next()
        else
          continue;

      statsCommit();
    until(not players[currentPlayer].isActive);

    if (not players[currentPlayer].isActive) then
      next('Set false in SRL');

  until(players.getActive() <= 0);
end;

procedure terminate();
begin
  progressReport();
end;

begin
  clearDebug();
  signature();
  writeln('Loading, please wait...');
  disguise('Spells Cast: 0');
  {$IFNDEF SMART}
  activateClient();
  {$ENDIF}
  setScriptProp(sp_WriteTimeStamp, [true]);
  addOnTerminate('terminate');
  setSupressExceptions(false);
  disableSRLLog := (not SRL_LOG);
  disableIPScreenshots := (not IP_SCREEN);
  disableSRLDebug := (not SRL_DEBUG);
  SRL_Events[EVENT_LOGIN] := @onLogin;
  smartShowConsole := false;

  setupSRL();
  statsSetup(534, STATS_ID, STATS_PASSWORD);

  setupScript();
  mainloop();
end.
